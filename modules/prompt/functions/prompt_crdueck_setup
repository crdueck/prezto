#
# A simple theme that displays relevant, contextual information.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/AzjmpwM.png
#

# Load dependencies.
pmodload 'helper'

function prompt_crdueck_pwd {
  local pwd="${PWD/#$HOME/~}"

  if [[ "$pwd" == (#m)[/~] ]]; then
    _prompt_crdueck_pwd="$MATCH"
    unset MATCH
  else
    _prompt_crdueck_pwd="${${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}//\%/%%}/${${pwd:t}//\%/%%}"
  fi
}

function prompt_crdueck_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_crdueck_pwd

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_crdueck_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_crdueck_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}¬ª%F{yellow}¬ª%F{green}¬ª%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}‚ô∫%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}¬´%F{yellow}¬´%F{red}¬´%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format ':%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}+%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}‚Üë%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}‚Üì%f%%b'
  zstyle ':prezto:module:git:info:branch' format ':%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format ':%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}*%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}¬±%f%%b'
  zstyle ':prezto:module:git:info:position' format ':%F{red}%p%f'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}üîí%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}‚ïê‚óè%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{red}‚óè%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{blue}git%f$(coalesce "%b" "%p" "%c")%s' \
    'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{red}%n%f@%F{yellow}%m%f "}%F{cyan}${_prompt_crdueck_pwd}%f${git_info:+${(e)git_info[prompt]}}%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}${VIM:+" %B%F{green}ViM%f%b"}${INSIDE_EMACS:+" %B%F{green}E%f%b"}${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_crdueck_setup "$@"
